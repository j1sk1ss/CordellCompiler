:
Brainfuck interpriter on CPL
For work test, input "++++++++[>++++++++<-]>+." sequence. Interpriter should return 'A' character.
Also hello world program
++++++++[>++++[>++>+++>+++>+<<<<-]>+>+>->>+[<]<-]>>.>---.+++++++..+++.>>.<-.<.+++.------.--------.>>+.>++.
:

start
    str startBrk = "start ["; : 7 :
    str endBrk = "end ]"; : 5 :

    arr code 10000 1 =;
    syscall 3 0 code 139;
    arr brk 1 1 = 32;

    int codelength = 0;
    while code[codelength] != brk[0]; lstart
        codelength = codelength + 1;
    lend

    : syscall 4 1 code codelength; :
    : syscall 4 1 startBrk 7; :
    : syscall 4 1 endBrk 5; :
    arr codes 2 1 = 91 93;

    arr bracketmap 10000 4 =;
    arr stack 10000 4 =;
    int stackptr = 0;
    int pos = 0;


    while pos < codelength; lstart
        int c = code[pos];
        int handled = 0;

        if code[pos] == codes[0]; ifstart
            stack[stackptr] = pos;
            stackptr = stackptr + 1;
            handled = 1;
            syscall 4 1 startBrk 7;
        ifend
        
        if handled == 0; ifstart
            if code[pos] == codes[1]; ifstart
                syscall 4 1 endBrk 5;
                if stackptr > 0; ifstart
                    stackptr = stackptr - 1;
                    int matchpos = stack[stackptr];
                    bracketmap[pos] = matchpos;
                    bracketmap[matchpos] = pos;
                ifend

                handled = 1;
            ifend
        ifend
        
        pos = pos + 1;
    lend

    arr tape 30000 1 =;
    int pointer = 0;
    int pc = 0;

    while pc < codelength; lstart
        int cmd = code[pc];
        int processed = 0;
        
        if cmd == 62; ifstart
            pointer = pointer + 1;
            pc = pc + 1;
            processed = 1;
        ifend

        if processed == 0; ifstart
            if cmd == 60; ifstart
                pointer = pointer - 1;
                pc = pc + 1;
                processed = 1;
            ifend
        ifend

        if processed == 0; ifstart
            if cmd == 43; ifstart
                tape[pointer] = tape[pointer] + 1;
                pc = pc + 1;
                processed = 1;
            ifend
        ifend
        
        if processed == 0; ifstart
            if cmd == 45; ifstart
                tape[pointer] = tape[pointer] - 1;
                pc = pc + 1;
                processed = 1;
            ifend
        ifend
        
        if processed == 0; ifstart
            if cmd == 46; ifstart
                arr fbuf 2 1 =;
                fbuf[0] = tape[pointer];
                syscall 4 1 fbuf 1;
                pc = pc + 1;
                processed = 1;
            ifend
        ifend
        
        if processed == 0; ifstart
            if cmd == 44; ifstart
                arr sbuf 2 1 =;
                syscall 3 0 sbuf 1;
                tape[pointer] = sbuf[0];
                pc = pc + 1;
                processed = 1;
            ifend
        ifend
        
        if processed == 0; ifstart
            if cmd == 91; ifstart
                if tape[pointer] == 0; ifstart
                    pc = bracketmap[pc];
                ifend
                if tape[pointer] != 0; ifstart
                    pc = pc + 1;
                ifend
                processed = 1;
            ifend
        ifend
        
        if processed == 0; ifstart
            if cmd == 93; ifstart
                int temppc = pc;
                if tape[pointer] != 0; ifstart
                    pc = bracketmap[temppc];
                ifend
                if tape[pointer] == 0; ifstart
                    pc = temppc + 1;
                ifend
                processed = 1;
            ifend
        ifend
        
        if processed == 0; ifstart
            pc = pc + 1;
        ifend
    lend

exit 0;